"""
    Copyright 2017 Inmanta

    Licensed under the Apache License, Version 2.0 (the "License");
    you may not use this file except in compliance with the License.
    You may obtain a copy of the License at

        http://www.apache.org/licenses/LICENSE-2.0

    Unless required by applicable law or agreed to in writing, software
    distributed under the License is distributed on an "AS IS" BASIS,
    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
    See the License for the specific language governing permissions and
    limitations under the License.

    Contact: code@inmanta.com
"""

typedef date as string matching std::validate_type("datetime.date", self)
"""
    A date expressed in the following format: ``YYYY-MM-DD``
"""
typedef time as string matching std::validate_type("datetime.time", self)
"""
    A time expressed in the following format: ``HH:MM[:SS[.ffffff]]``
"""
typedef datetime as string matching std::validate_type("datetime.datetime", self)
"""
    A date and time combination expressed in the following format: ``YYYY-MM-DD[T]HH:MM[:SS[.ffffff]][Z[Â±]HH[:]MM]]]``
"""
typedef ipv4_address as string matching std::validate_type("ipaddress.IPv4Address", self)
"""
    A string representing a valid IPv4 address (e.g. 192.168.0.1).
"""
typedef ipv4_interface as string matching std::validate_type("ipaddress.IPv4Interface", self)
"""
    A string representing an IPv4 interface. This type is equivalent to ipv4_network,
    except that arbitrary host addresses are accepted.
"""
typedef ipv4_network as string matching std::validate_type("ipaddress.IPv4Network", self)
"""
    A string consisting of an IPv4 network address and an optional mask, separated by a slash (/). The mask can
    be either a single number, or a string representation of an IPv4 address.

    Examples: 192.168.1.0/24, 192.168.1.0/255.255.255.0 and 192.168.1.0/0.0.0.255.
"""
typedef ipv6_address as string matching std::validate_type("ipaddress.IPv6Address", self)
"""
    A string representing a valid IPv6 address.

    Example: 2001:db8::1000
"""
typedef ipv6_interface as string matching std::validate_type("ipaddress.IPv6Interface", self)
"""
    A string representing an IPv6 interface. This type is equivalent to ipv6_network,
    except that arbitrary host addresses are accepted.
"""
typedef ipv6_network as string matching std::validate_type("ipaddress.IPv6Network", self)
"""
    A string consisting of an IPv6 address and an optional prefix length, separated by a slash (/). The IP address
    is the network address, and the prefix length must be a single number, the prefix.

    Example: 2001:db00::0/24
"""
typedef uuid as string matching std::validate_type("uuid.UUID", self)
"""
    A string repesenting a uuid.
"""
typedef email_str as string matching std::validate_type("pydantic.EmailStr", self)
"""
    A string representing a valid email address.
"""
typedef name_email as string matching std::validate_type("pydantic.NameEmail", self)
"""
    A string consisting of a name and email address combination.

    Example: Fred Bloggs <fred.bloggs@example.com>
"""
typedef any_url as string matching std::validate_type("pydantic.AnyUrl", self)
"""
    A string representing a URL.
"""
typedef any_http_url as string matching std::validate_type("pydantic.AnyHttpUrl", self)
"""
    A string representing a URL with scheme http or https.
"""
typedef http_url as string matching std::validate_type("pydantic.HttpUrl", self)
"""
    A string representing a URL with the following constraints:
        * scheme: http or https
        * Top-level domain name must be present in the hostname
        * Maximum length of 2083 characters
"""
typedef ipv_any_address as string matching std::validate_type("pydantic.IPvAnyAddress", self)
"""
    A string representing a valid IPv4 or IPv6 address.
"""
typedef ipv_any_interface as string matching std::validate_type("pydantic.IPvAnyInterface", self)
"""
    A string matching the requiremenst of ipv4_interface or ipv6_interface.
"""
typedef ipv_any_network as string matching std::validate_type("pydantic.IPvAnyNetwork", self)
"""
    A string matching the requiremenst of ipv4_network or ipv6_network.
"""
typedef negative_float as float matching std::validate_type("pydantic.NegativeFloat", self)
"""
    A floating point number less than zero.
"""
typedef negative_int as int matching std::validate_type("pydantic.NegativeInt", self)
"""
    A integer number less than zero.
"""
typedef positive_float as float matching std::validate_type("pydantic.PositiveFloat", self)
"""
    A floating point number greater than zero.
"""
typedef positive_int as int matching std::validate_type("pydantic.PositiveInt", self)
"""
    A integer number greater than zero.
"""
typedef alfanum as string matching std::validate_type("pydantic.constr", self, {"regex": "^[a-zA-Z0-9]*$", "strict": true})
"""
    An alfanumeric number (lower- and uppercase characters are allowed).
"""
typedef base64 as string matching std::is_base64_encoded(self)
"""
    A string representing a base64-encoded string.
"""
typedef printable_ascii as string matching std::validate_type(
    "pydantic.constr", self, {"regex": "^[\x20-\x7e]*$", "strict": true}
)
"""
    A string consisting of only printable ascii characters: from <whitespace> to ~
"""
typedef ascii_word as string matching std::validate_type(
    "pydantic.constr", self, {"regex": "^[\x21-\x7e]*$", "strict": true}
)
"""
    A string consisting of only printable non-whitespace ascii characters: from ! to ~
"""

typedef non_empty_string as string matching /^(.*\S.*)$/
"""
    A string that contains at least one non-whitespace character
"""

typedef port as int matching self >= 0 and self < 65536
"""
    A TCP/UDP port number.
"""

implementation none for Entity:
    """
        An empty implementation that can be used as a safe default.
    """
end

entity Resource:
    """
        A base entity for resources that can be exported. This type add specific attributes
        that are common for most handlers.
        It is not required to inherit from this entity at the moment but highly recommended for documentation purposes.

        :attr send_event: This controls whether a resource should send its deploy state to the resources in its provides.
        :attr receive_events: This controls whether a resource should receive events from other resources, i.e. its requires
            that have send_event=true. Resources with receive_events=true will always be deployed whenever a dependency
            produces an event. Set this to false for resources that do not rely on events (most practical resources) to
            reduce the amount of redundant deploys, thereby increasing performance (will not have any effect for
            inmanta-core<14).
        :attr report_only: This resource should only report compliance, not enforce any state. Will not have any effect for
            inmanta-core<18 (inmanta-service-orchestrator<9).
    """
    bool send_event=true
    bool receive_events=true  # defaults to true for backwards compatibility, to be changed in iso9
    bool report_only=false
end

entity PurgeableResource extends Resource:
    """
        A base class for a resource that can be purged and can be purged by Inmanta whenever the resource is no
        longer managed.

        :attr purged: Set whether this resource should exist or not.
        :attr purge_on_delete: Purge the resource when it is deleted from the configuration model.
                               When this attribute is true, the server will include a resource with
                               purged=true when this resource is no longer included in the
                               configuration model.
    """
    bool purged=false
    bool purge_on_delete=false
end

entity DiscoveryResource extends Resource:
    """
    A resource that scans the infrastructure for a certain type of deployment.
    This resource can be used to facilitate the onboarding of resources that are not yet managed
    by the orchestrator.
    """
end

entity ManagedResource extends Resource:
    """
        A base class for a resource that can be ignored/unmanaged by Inmanta.

        :attr managed: This determines whether this resource is managed by Inmanta or not.
    """
    bool managed=true
end

entity ManagedDevice:
    """
        This interface represents all devices that can be managed
    """
    hoststring  name
end

entity Dataclass:
    """
        Super class for inmanta entities that have a python counterpart.

        The python counterpart is expect to be
        - a frozen dataclass,
        - with the same name
        - in the plugins package of this module
        - in the corresponding submodule
        - with the exact same fields

        The Inmanta entity is expect to
        - have no relations
        - have no indexes
        - have only std::none as implementation

    """
end

entity Host extends ManagedDevice:
    """
        A host models a server or computer in the managed infrastructure that
        has an ip address.

        :attr ip: The ipaddress of this node.
        :attr remote_agent: Start the mgmt agent for this node on the server and use remote io (ssh).
        :attr remote_user: The remote user for the remote agent to login with.
        :attr remote_port: The remote port for this remote agent to use.
    """
    ipv_any_address? ip = null
    bool remote_agent=false
    string remote_user="root"
    port remote_port=22
end

implement Host using hostDefaults

index Host(name)

typedef hoststring as string matching /^[A-Za-z0-9-]+(\.[A-Za-z0-9-]+)*$/


# define operating systems
unix = OS(name = "unix")
windows = OS(name = "windows")

linux = OS(name = "linux", family = unix)
solaris = OS(name = "solaris", family = unix)
freebsd = OS(name = "freebsd", family = unix)
openbsd = OS(name = "openbsd", family = unix)
macos = OS(name = "macos", family = unix)

entity OS:
    """
        Defines an operating system

        :attr name: The name of the operating system or family of operating systems
        :attr version: A specific version
        :attr python_cmd: Specifies what command should be used to launch the python interpreter on the other end
    """
    string name
    float version = 0.0
    string? python_cmd = "python"
end

implement OS using std::none

Host.os [1] -- OS
"""
    Each host has an OS defined. This values is mostly used to select implementation in the
    where clause of an `implement` statement. The :py:func:`familyof` plugin can be used
    for this.
"""

OS.family [0:1] -- OS.member [0:]

entity HostConfig:
    """
        This represents generic configuration for a host. This entity is used
        by other modules to include their host specific configuration. This
        should be instantiated in the implementation of std::Host or subclasses.
        This host specific configuration cannot be included by just implementing
        std::Host because possibly subclasses of std::Host are instantiated and
        implementations are not inherited.
    """
end

implementation agentConfig for HostConfig:
    std::AgentConfig(
        autostart=true,
        agentname=host.name,
        uri=std::template("std/host_uri.j2"),
        provides=host,
    )
end

Host.host_config [1] -- HostConfig.host [1]
implement HostConfig using std::none
implement HostConfig using agentConfig when host.ip != null and host.remote_agent

implementation hostDefaults for std::Host:
    HostConfig(host=self)
end


entity HostGroup:
    """
        This entity represents a group of hosts. For example a cluster of machines.
    """
    string name
end

Host.host_groups [0:] -- HostGroup.hosts [0:]

implement HostGroup using std::none


typedef config_agent as string matching self != "internal"
"""
    Type for configurable agents.
"""

entity AgentConfig extends PurgeableResource:
    """
        Control agent settings. Currently these settings are only applied to autostarted agents

        :attr autostart: When this flag is set to true, the resource will be exported and set the agent map
                          on the orchestrator. When false, this instance is ignored but can be used to generate
                          agent configuration files.
        :attr agentname: The name of the agent to which this config applies.
        :attr agent: If a resource is exported, agent manages the resource.
        :attr uri: The uri that indicates how the agent should execute. Currently the following uri are supported:
                   * "" An empty string. This is the same as running it locally
                   * local: Manage resource locally
                   * ssh://[user@]hostname[:port] Login using ssh. When user is left out, root is assumed. For port, the system
                     default is used.
                   * host The actual hostname or ip to use. Although this is not a valid host in uri form it is supported.
                   * A query string can be used to set the properties:
                        * python: The python interpreter to use. The default value is python
                        * retries: The number of retries before giving up. The default number of retries 10
                        * retry_wait: The time to wait between retries for the remote target to become available. The default wait is 30s.
                   Example: ssh://centos@centos-machine/?python=python3 (This would connect to a the centos machine and use python3 as it's interpreter)
    """
    bool autostart = false
    config_agent agentname
    string agent="internal"
    string uri="local:"
end

implement AgentConfig using std::none

index AgentConfig(agentname)


entity MutableString:
    """
Wrapper for string values. It can be used to pass a string out of an if statement, or to
use relations to create a mutable set of strings.

:Example:
.. code-block:: inmanta

    attr_a = std::MutableString()
    if some_condition:
        attr_a.value = "a"
    else:
        attr_a.value = "b"
    end


:Example:
.. code-block:: inmanta

    entity Test:
    end

    Test.string_list [0:] -- std::MutableString

    a = Test()
    a.string_list += std::MutableString(value="value1")
    a.string_list += std::MutableString(value="value2")
"""
  string? value
end

implement MutableString using std::none


entity MutableInt:
    """
Wrapper for int values, used to pass a int out of an if statement or to
use relations to create a mutuable set of int.

:Example:
.. code-block:: inmanta

    attr_a = std::MutableInt()
    if some_condition:
        attr_a.value = 3
    else:
        attr_a.value = 4
    end

:Example:
.. code-block:: inmanta

    entity Test:
    end

    Test.string_list [0:] -- std::MutableInt

    a = Test()
    a.string_list += std::MutableInt(value=3)
    a.string_list += std::MutableInt(value=7)
"""
  int? value
end

implement MutableInt using std::none


entity MutableFloat:
    """
Wrapper for float values, used to pass a float out of an if statement or to
use relations to create a mutable set of float.

:Example:
.. code-block:: inmanta

    attr_a = std::MutableFloat()
    if some_condition:
        attr_a.value = 3.0
    else:
        attr_a.value = 4.0
    end

:Example:
.. code-block:: inmanta

    entity Test:
    end

    Test.string_list [0:] -- std::MutableFloat

    a = Test()
    a.string_list += std::MutableFloat(value=3.0)
    a.string_list += std::MutableFloat(value=7.0)
"""
  float? value
end

implement MutableFloat using std::none


entity MutableBool:
    """
Wrapper for boolean values, used to pass a boolean out of an if statement.

:Example:
.. code-block:: inmanta

    attr_a = std::MutableBool()
    if some_condition:
        attr_a.value = True
    else:
        attr_a.value = Null
    end

"""
  bool? value
end

implement MutableBool using std::none

entity ResourceSet:
    """
    A ResourceSet describes resources that logically belong together,
    and can be manipulated independently from other managed resources.

    :attr name: The name of the resource set.
    """
    non_empty_string name
end

ResourceSet.resources [0:] -- std::Resource

index ResourceSet(name)

implement ResourceSet using std::none
