from libc.stddef cimport size_t
from libc.stdint cimport int64_t, uint32_t, uint64_t
from libcpp cimport bool as cpp_bool
from libcpp.string cimport string
from libcpp.vector cimport vector

from rdbpy.slice_ cimport Slice
from rdbpy.snapshot cimport Snapshot
from rdbpy.status cimport Status
from rdbpy.std_memory cimport shared_ptr

cimport rdbpy.db as db
cimport rdbpy.iterator as iterator
cimport rdbpy.options as options


cdef extern from "rocksdb/utilities/transaction_db_mutex.h" namespace "rocksdb":
    cdef cppclass TransactionDBMutexFactory:
        pass


cdef extern from "rocksdb/utilities/transaction_db.h" namespace "rocksdb":
    ctypedef uint64_t TransactionID
    ctypedef string TransactionName

    cdef enum TxnDBWritePolicy:
        WRITE_COMMITTED
        WRITE_PREPARED
        WRITE_UNPREPARED

    cdef cppclass LockManager:
        pass

    cdef cppclass LockManagerHandle:
        LockManager* getLockManager() nogil except+

    cdef cppclass TransactionDBOptions:
        TransactionDBOptions() nogil except+
        int64_t max_num_locks
        uint32_t max_num_deadlocks
        size_t num_stripes
        int64_t transaction_lock_timeout
        int64_t default_lock_timeout
        shared_ptr[TransactionDBMutexFactory] custom_mutex_factory
        TxnDBWritePolicy write_policy
        cpp_bool rollback_merge_operands
        shared_ptr[LockManagerHandle] lock_mgr_handle
        cpp_bool skip_concurrency_control
        int64_t default_write_batch_flush_threshold

    cdef cppclass TransactionDBWriteOptimizations:
        TransactionDBWriteOptimizations() nogil except+
        cpp_bool skip_concurrency_control
        cpp_bool skip_duplicate_key_check

    cdef cppclass TransactionOptions:
        TransactionOptions() nogil except+
        cpp_bool set_snapshot
        cpp_bool deadlock_detect
        cpp_bool use_only_the_last_commit_time_batch_for_recovery
        int64_t lock_timeout
        int64_t expiration
        int64_t deadlock_detect_depth
        size_t max_write_batch_size
        cpp_bool skip_concurrency_control
        cpp_bool skip_prepare
        int64_t write_batch_flush_threshold

    cdef cppclass Transaction:
        Status Commit() nogil except+
        Status Rollback() nogil except+
        Status Prepare() nogil except+
        void SetSnapshot() nogil except+
        const Snapshot* GetSnapshot() nogil except+
        void ClearSnapshot() nogil except+
        Status SetName(const TransactionName&) nogil except+
        TransactionName GetName() nogil except+
        TransactionID GetID() nogil except+
        void SetSavePoint() nogil except+
        Status PopSavePoint() nogil except+
        Status RollbackToSavePoint() nogil except+
        void DisableIndexing() nogil except+
        void EnableIndexing() nogil except+
        Status Put(db.ColumnFamilyHandle*, const Slice&, const Slice&, cpp_bool assume_tracked) nogil except+
        Status Put(const Slice&, const Slice&) nogil except+
        Status Merge(db.ColumnFamilyHandle*, const Slice&, const Slice&, cpp_bool assume_tracked) nogil except+
        Status Merge(const Slice&, const Slice&) nogil except+
        Status Delete(db.ColumnFamilyHandle*, const Slice&, cpp_bool assume_tracked) nogil except+
        Status Delete(const Slice&) nogil except+
        Status SingleDelete(db.ColumnFamilyHandle*, const Slice&, cpp_bool assume_tracked) nogil except+
        Status SingleDelete(const Slice&) nogil except+
        Status Get(const options.ReadOptions&, db.ColumnFamilyHandle*, const Slice&, string*) nogil except+
        Status Get(const options.ReadOptions&, const Slice&, string*) nogil except+
        vector[Status] MultiGet(
            const options.ReadOptions&,
            const vector[db.ColumnFamilyHandle*]&,
            const vector[Slice]&,
            vector[string]*) nogil except+
        vector[Status] MultiGet(
            const options.ReadOptions&,
            const vector[Slice]&,
            vector[string]*) nogil except+
        iterator.Iterator* GetIterator(const options.ReadOptions&) nogil except+
        iterator.Iterator* GetIterator(const options.ReadOptions&, db.ColumnFamilyHandle*) nogil except+

    cdef cppclass CppTransactionDB "rocksdb::TransactionDB"(db.DB):
        Transaction* BeginTransaction(
            const options.WriteOptions&,
            const TransactionOptions&,
            Transaction* old_txn) nogil except+
        Transaction* BeginTransaction(
            const options.WriteOptions&,
            const TransactionOptions&) nogil except+
        Transaction* GetTransactionByName(const TransactionName&) nogil except+
        void GetAllPreparedTransactions(vector[Transaction*]*) nogil except+

    cdef Status TransactionDB_Open "rocksdb::TransactionDB::Open"(
        const options.Options&,
        const TransactionDBOptions&,
        const string&,
        CppTransactionDB**) nogil except+

    cdef Status TransactionDB_Open_CF "rocksdb::TransactionDB::Open"(
        const options.DBOptions&,
        const TransactionDBOptions&,
        const string&,
        const vector[db.ColumnFamilyDescriptor]&,
        vector[db.ColumnFamilyHandle*]*,
        CppTransactionDB**) nogil except+


# Helper functions to disambiguate overloaded Transaction methods
cdef extern from "rocksdb/utilities/transaction.h":
    """
    static inline rocksdb::Iterator* Transaction_GetIterator(
        rocksdb::Transaction* txn,
        const rocksdb::ReadOptions& opts) {
        return txn->GetIterator(opts);
    }

    static inline rocksdb::Iterator* Transaction_GetIterator_CF(
        rocksdb::Transaction* txn,
        const rocksdb::ReadOptions& opts,
        rocksdb::ColumnFamilyHandle* cf) {
        return txn->GetIterator(opts, cf);
    }
    """
    iterator.Iterator* Transaction_GetIterator(
        Transaction*,
        const options.ReadOptions&) nogil except+
    iterator.Iterator* Transaction_GetIterator_CF(
        Transaction*,
        const options.ReadOptions&,
        db.ColumnFamilyHandle*) nogil except+
